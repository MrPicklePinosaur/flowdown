
WHITESPACE = _{ " " }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

diagram = {
    SOI ~
    (stmt? ~ NEWLINE)* ~
    EOI
}

stmt = {
    dialog_stmt|bookmark_stmt|choice_stmt|inline_stmt
}
inline_stmt = {
    command_stmt|jump_stmt|utterance_stmt
}

dialog_stmt = {
    "@" ~ SPACE_ID
}

bookmark_stmt = {
    "=" ~ SPACE_ID
}

command_stmt = {
    "[" ~ (end_command_body|set_command_body|capture_command_body|code_command_body) ~ "]"
}
end_command_body = {
    "end"
}
set_command_body = {
    "set" ~ variable_identifier ~ string_literal
}
capture_command_body = {
    "capture" ~ variable_identifier
}
code_command_body = {
    "code" ~ js_file_name
}

conditional = {
    (variable_identifier|string_literal) ~ operator ~ (variable_identifier|string_literal)
}
operator = {
    "=="|"!="
}

choice_stmt = {
    "*" ~ conditional ~ ":" ~ inline_stmt
}

utterance_stmt = {
    utterance_content ~ ("#" ~ utterance_voice)?
}
utterance_content = {
    (ASCII_ALPHANUMERIC|"!"|"?")+
}
utterance_voice = @{
    (ASCII_ALPHANUMERIC|"-")+
}

jump_stmt = {
    "->" ~ (dialog_identifier|bookmark_identifier)
}

dialog_identifier = {
    "@" ~ SPACE_ID
}
bookmark_identifier = {
    SPACE_ID
}
variable_identifier = @{
    "$" ~ ID
}
string_literal = @{
    QUOTATION_MARK ~ SPACE_ID ~ QUOTATION_MARK
}

js_file_name = @{
    ASCII_ALPHANUMERIC+ ~ ".js"    
}

SPACE_ID = @{
    (ASCII_ALPHANUMERIC|WHITESPACE)+
}

ID = @{
    ASCII_ALPHANUMERIC+
}
